#
# Copyright (C) 2022 Vaticle
#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#

define

### Attributes ###

    name sub attribute, value string;
    date sub attribute, value datetime;
    description sub attribute, value string;
    id sub attribute, value string;

    price sub attribute, value long;
    stock sub attribute, value long;

    ISBN sub attribute, value string;
    Book_Author sub attribute, value string;
    Publisher sub attribute, value string;

    rating sub attribute, value long;

    age sub attribute, value long;

    foreign-user-id sub attribute, value string;
    status sub attribute, value string;
    delivery_address sub attribute, value string;
    payment_details sub attribute, value string;

    username sub attribute, value string;
    password sub attribute, value string;
    foreign-id sub attribute, value string;

    genre sub attribute, value string,
        plays taging:tag,
        plays tag-hierarchy:sup-tag,
        plays tag-hierarchy:sub-tag;

### Entities ###
    Product sub entity, abstract,
        owns id @key,
        owns name,
        owns description,
        owns price,
        owns stock,
        plays ordering:item,
        plays reviewing:product;

    Book sub Product,
        owns ISBN,
        owns Book_Author,
        owns Publisher,
        plays taging:book;

    Person sub entity,
        owns name,
        plays reviewing:author;

    User sub Person,
        owns id @key,
        owns foreign-id,
        owns username,
        owns password,
        owns age,
        plays ordering:author;

    Order sub entity,
        owns id @key,
        owns foreign-user-id,
        owns date,
        owns status,
        owns delivery_address,
        owns payment_details,
        plays ordering:order;

### Relations ###
    reviewing sub relation,
        owns rating,
        relates author,
        relates product;

    taging sub relation,
        relates tag,
        relates book;

    ordering sub relation,
        relates order,
        relates item,
        relates author;

    tag-hierarchy sub relation,
        relates sub-tag,
        relates sup-tag;
        
##### RULES #####

    rule super-tag-hierarchy:
        when {
            $b isa genre;
            $p isa genre;
            $bb isa genre;
            (sup-tag: $p, sub-tag: $b) isa tag-hierarchy;
            (sup-tag: $b, sub-tag: $bb) isa tag-hierarchy;
        } then {
            (sup-tag: $p, sub-tag: $bb) isa tag-hierarchy;
        };
